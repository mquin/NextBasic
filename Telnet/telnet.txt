#program telnet
  10 CLEAR 40959
  20 d=0 :; set to 1 to enable status lines
  30 ; To use a HTTP CONNECT proxy, configure it here
  40 proxy$=""
  50 pport=3128
  60 rtmo=512 :; receive time out
  70 stmo=128 :; send time out
  80 LAYER 1,2
  90 BORDER 0: PAPER 0: INK 7: CLS 
 100 RUN AT 1
 110 INPUT "host", host$ : PRINT 
 120 IF host$="" THEN PRINT ;"Missing host name." : GO TO %110
 130 INPUT "port, or return for 23", p
 140 IF p=0 THEN p=23
 150 GO SUB %840: REM Open Channel and install if needed
 160 CLS 
 170 ON ERROR GO TO %810
 180 IF d=1  
 190   OPEN # 6,"w>0,0,22,32,5"
 200   OPEN # 5,"w>21,0,2,32,5"
 210   OPEN # 8,"w>22,0,2,32,5"
 220 ELSE 
 230   OPEN # 6,"w>0,0,24,32,5"
 240 ENDIF 
 250 PRINT #6;"ESPAT BASIC terminal"
 260 PRINT #6;"Press BREAK to exit."
 270 q=0
 280 r=0
 290 IF proxy$=""
 300 PROC tcpconnect(host$,p)
 310 ELSE 
 320 PROC tcpconnect(proxy$,pport)
 330 PRINT #7; "connect " ; host$ ; ":" ; p ; " HTTP/1.0" ; CHR$ (10) ; CHR$ (10) ;
 340 ENDIF 
 350 ON ERROR GO TO %800
 360 DRIVER 78,3,128 TO %f,%i,%o: REM will clear flags
 370 DIM #7 TO %s
 380 IF d=1 THEN PRINT #5; AT 1,0;%s;"    ";%i;",";%o;",";%f;"    ";q;"    ";
 390 IF %s=0 THEN GO TO %710
 400 NEXT #7 TO c
 410 IF q=1 THEN GO TO %450
 420 IF q=2 THEN GO TO %460
 430 IF c=255 AND q=0 THEN q=1 : GO TO %360
 440 GO TO %610
 450 IF c > 250 THEN t=c : q=2 : GO TO %360
 460 IF d=1 THEN PRINT #8; AT 1,0 ;c ; "    ";
 470   IF t=251 OR t=252
 480     IF c=1 :; ACCEPT ECHO
 490       PRINT #7; CHR$ 255; CHR$ 253; CHR$ c;
 500       q=0 : GO TO %360
 510     ELSE 
 520       PRINT #7; CHR$ 255; CHR$ 254; CHR$ c; :; DONT
 530       q=0 : GO TO %360
 540     ENDIF 
 550   ELSE IF t=253 OR t=254
 560     PRINT #7; CHR$ 255; CHR$ 252+ CHR$ c; :; WONT
 570     q=0 : GO TO %360
 580   ENDIF 
 590 t=0
 600 ENDIF 
 610 q=0
 620 IF c=10 THEN PRINT #6: LET c=13: GO TO %710
 630 IF c = 13 THEN GO TO %710
 640 ; Incomplete filtering of ANSI escape sequences
 650 IF c=27 AND r=0 THEN r=1 : GO TO %360
 660 IF c=91 AND r=1 THEN r=2 : GO TO %360
 670 IF (c>$3f AND c < $7f) AND r=2 THEN r=0 : GO TO %360
 680 IF (c>$2f AND c < $40) AND r=2 THEN GO TO %360
 690 IF (c>$1f AND c < $30) AND r=2 THEN GO TO %360
 700 PRINT #6; CHR$ (c); : IF c=0 THEN PRINT #6;"_";
 710 IF INKEY$ ="" THEN GO TO %360
 720 LET c$= INKEY$ 
 730 IF INKEY$ <>"" THEN GO TO %730
 740 ;PRINT #5;c$; : REM local echo
 750 ;IF c$= CHR$ (13) THEN PRINT #7; CHR$ (10); : GO TO %320: REM CR/LF for unix use
 760 IF c$= CHR$ (12) THEN PRINT #7; CHR$ (127); : GO TO %360: REM DELETE code
 770 IF c$= CHR$ (7) THEN PRINT #7; CHR$ (27); : GO TO %360: REM EDIT as ESCAPE
 780 PRINT #7;c$;
 790 GO TO %360
 800 INPUT "Break ?";a$: IF a$<>"y" AND a$<>"Y" THEN GO TO %360
 810 ON ERROR : CLOSE # 15: CLOSE # 8 : CLOSE # 7: CLOSE # 6: CLOSE # 5
 820 .uninstall /nextzxos/espat.drv
 830 STOP 
 840 REM See if we can open network if not install drivers
 850 ON ERROR GO TO %890
 860 OPEN # 15,"D>N"
 870 ON ERROR GO TO %800
 880 RETURN 
 890 PRINT "Installing driver in main memory as not loaded."
 900 .install /nextzxos/espat.drv
 910 PRINT "Installed ESPAT.DRV"
 920 LET %n=78
 930 LET %m=0: PRINT "ESPAT.SYS Load page ";%m
 940 LOAD "/nextzxos/espat.sys" CODE 40960: REM IN main memory
 950 DRIVER %n,1,%m: REM 0 to allocate main memory, patch and initialise
 960 BANK NEW %b: PRINT "Allocating 1 buffer ";%b
 970 DRIVER %n,6,%b
 980 DRIVER %n,9,0 TO %p: PRINT "115K Baud - prescaler ";%p
 990 DRIVER %n,3: PRINT "IPD Driver started"
1000 DRIVER %n,5,rtmo,stmo
1010 RETURN 
1020 DEFPROC tcpconnect(n$,p)
1030 OPEN # 7,"D>N>TCP,"+n$+","+ STR$ (p)
1040 DRIVER 78,10,1,1: REM immediate send
1050 ENDPROC 
